SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='$#,##0.00;-$#,##0.00';
SET TimeFormat='h:mm:ss TT';
SET DateFormat='YYYY-MM-DD';
SET TimestampFormat='YYYY-MM-DD h:mm:ss[.fff] TT';
SET FirstWeekDay=6;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='en-US';
SET CreateSearchIndexOnReload=1;
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET NumericalAbbreviation='3:k;6:M;9:G;12:T;15:P;18:E;21:Z;24:Y;-3:m;-6:Î¼;-9:n;-12:p;-15:f;-18:a;-21:z;-24:y';

let curr_conn = 'your connection name here';
let filepath = 'your filepath here'

Sub CurrencyCombinations
    //----------------------------------------------------------|
    // the below list gets joined to itself in order to create  |
    // a list of all curency combinations that need to be       |
    // looked up for the conversion table.                      |
    //----------------------------------------------------------|


    CURRCMB_TMP:
    Load * inline [
    FROM,
    CAD,
    CNY,
    EUR,
    GBP,
    USD,
    ];

    Join
    Load
        FROM as TO
    Resident CURRCMB_TMP;


    NoConcatenate
    CurrencyCodes:
    Load
        *,
        RowNo() as RowNum
    Resident CURRCMB_TMP;
    Drop Table CURRCMB_TMP;

    Let vFinalRow = Peek('RowNum',-1);
    Trace Final Row is $(vFinalRow);
End Sub;

Sub CurrencyConvert(vFrom,vTo)
    Let ErrorMode = 0;
    //-------------------------------------------------------------|
    // this script looks up the currency conversion rate by date   |
    // and code, then stores it out to a QVD for later merging     |
    // into one master file for loading into some other app        |
    //-------------------------------------------------------------|

    Trace ------------Converting $(vFrom) into $(vTo)--------------;

    '$(vFrom)_$(vTo)':
    LOAD
        *,
        '$(vFrom)'	as FromCurr,
        '$(vTo)'	as ToCurr
    FROM [$(curr_conn)]
    (html,URL is 'https://www.currency-converter.org.uk/currency-rates/historical/table/$(vFrom)-$(vTo).html', 
        codepage is 28591, embedded labels, table is @1);
        
    Let a = FieldName(3,'$(vFrom)_$(vTo)');
    Let b = FieldName(4,'$(vFrom)_$(vTo)');
    Trace $(a);
    Trace $(b);
    Rename Field '$(a)' to FromCurrRate;
    Rename Field '$(b)' to ToCurrRate;

    Store '$(vFrom)_$(vTo)' into [lib://$(filepath)\CUR_LOOKUP_$(vFrom)-$(vTo).qvd](qvd);
    Drop Table '$(vFrom)_$(vTo)';
    Let ErrorMode = 1;
End Sub;

Sub ExtractRates
    //------------------------------------------------------------|
    // this section pulls a from and to currency, then calls the  |
    // lookup subroutine, iterating throug all combinations       |
    //------------------------------------------------------------|

    Let vCurrentRow = 1;

    Do
    NoConcatenate
    CRC_TMP:
    Load * Resident CurrencyCodes;

    Let vFCurr = Peek('FROM',$(vCurrentRow)-1);
    Let vTCurr = Peek('TO',$(vCurrentRow)-1);

    Drop Table CRC_TMP;
    Call CurrencyConvert('$(vFCurr)','$(vTCurr)');
    Let vCurrentRow = $(vCurrentRow)+1;
    Trace Current row is $(vCurrentRow) ---- final row is $(vFinalRow);
    Loop Until $(vCurrentRow) > $(vFinalRow);

End Sub;

Sub FinalTable
    //---------------------------------------------------------|
    // merge all currency conversion tables into one file that |
    // can be loaded into a currency conversion lookup app     |
    //---------------------------------------------------------|


    Set ErrorMode = 0;
    ConvTable:
    LOAD 
    //     "Historical Date" as WeekDay,
        Date(date#("Historical Date1",'DD/MM/YYYY')) as MarketConvDate,
    //     FromCurrRate,
        keepchar(ToCurrRate,'0123456789.') as MarketConvRate,
        //Analysis,
        FromCurr&' -> '&ToCurr as [Conversion Code],
        FromCurr,
        ToCurr    
    FROM [lib://$(filepath)\CUR_LOOKUP_*.qvd]
    (qvd);
    Set ErrorMode = 1;

    Store ConvTable into [lib://$(filepath)\z_Master_Conversion_Table.qvd](qvd);
    Drop Tables ConvTable,CurrencyCodes;


EndSub;


//----------------------------------------|
// first call the combination list, then  |
// call the rate iteration loop, finally  |
// call the Final Table merge             |
//----------------------------------------|

Call CurrencyCombinations;
Call ExtractRates;
	//Sub Conversion is called on a loop in Sub ExtractRates
Call FinalTable;